buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.1.0'
    }
}

plugins {
    id 'java'
    id 'publishing'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
    id 'com.jfrog.artifactory' version '3.2.0'
}

ext {
    gradleVersion = '2.9'
    gradleScriptDir = "${rootProject.projectDir}/gradle"

    // Libraries
    slf4jVersion = '1.7.12'
    logbackVersion = '1.1.3'
    jacksonVersion = '2.6.4'
    nettyVersion = '4.0.33.Final'

    // Riak PB
    riakPbVersion = '2.0.0.17-SNAPSHOT'

    bdpRiakClientVersion = '1.1.0'

    // Build Info
    bashoArtifactoryUrl = 'https://basholabs.artifactoryonline.com/basholabs'
    buildNum = System.getenv('TRAVIS_BUILD_NUMBER')
    buildId = System.getenv('TRAVIS_BUILD_ID')
    buildName = System.getenv('TRAVIS_REPO_SLUG')
    commitHash = System.getenv('TRAVIS_COMMIT')
    travisBuildLink = "https://travis-ci.com/${buildName}/builds/${buildId}"
    artifactoryUser = System.getenv('ARTIFACTORY_USER')
    artifactoryPass = System.getenv('ARTIFACTORY_PASS')

    // Testing
    mockitoVersion = '1.10.19'
    powermockVersion = '1.6.2'
}

project.archivesBaseName = 'dataplatform-riak-client'

[compileJava]*.options*.compilerArgs = [
        //"-Xlint:varargs",
        //"-Xlint:cast",
        //"-Xlint:classfile",
        //"-Xlint:dep-ann",
        //"-Xlint:divzero",
        //"-Xlint:empty",
        //"-Xlint:finally",
        //"-Xlint:overrides",
        //"-Xlint:path",
        //"-Xlint:processing",
        //"-Xlint:static",
        //"-Xlint:try",
        "-Xlint:deprecation",
        "-Xlint:unchecked",
        "-Xlint:-serial",      // intentionally disabled
        "-Xlint:-options",     // intentionally disabled
        "-Xlint:-fallthrough", // intentionally disabled
        "-Xlint:-rawtypes"     // TODO enable and fix warnings
]

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    // SLF4J Logging
    compile "org.slf4j:slf4j-api:$slf4jVersion"

    // Jackson JSON
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
            "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jacksonVersion"

    // Netty
    compile "io.netty:netty-all:$nettyVersion"

    // Riak Protobuf
    compile "com.basho.riak.protobuf:riak-pb:$riakPbVersion"

    // Copy all compile deps except for the shaded protobuf stuff
    shadow configurations.compile.allDependencies.findAll { it.name != 'riak-pb' }

    // Testing
    testCompile "org.mockito:mockito-core:$mockitoVersion",
            "org.powermock:powermock-api-mockito:$powermockVersion",
            "org.powermock:powermock-module-junit4:$powermockVersion",
            "com.jayway.awaitility:awaitility:1.5.0"
    testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
}

shadowJar {
    baseName = 'dataplatform-riak-client'
    classifier = ''
    dependencies {
        include(dependency('com.basho.riak.protobuf:riak-pb'))
        include(dependency('com.google.protobuf:protobuf-java'))
        relocate 'com.google.protobuf', 'shaded.com.google.protobuf'
        relocate 'com.basho.riak.protobuf', 'shaded.com.basho.riak.protobuf'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            pom.withXml {
                // add all items necessary for maven central publication
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'Riak Client for Java'
                    description 'Java client for Riak 2.0'
                    url 'https://github.com/basho/riak-java-client'

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                            comments 'A business-friendly OSS license'
                        }
                    }

                    developers {
                        developer {
                            name 'Brian Roach'
                            email 'roach@basho.com'
                            organization 'Basho Technologies, Inc'
                            organizationUrl 'http://www.basho.com'
                        }
                        developer {
                            name 'David Rusek'
                            email 'drusek@basho.com'
                            organization 'Basho Technologies, Inc'
                            organizationUrl 'http://www.basho.com'
                        }
                        developer {
                            name 'Alex Moore'
                            email 'amoore@basho.com'
                            organization 'Basho Technologies, Inc'
                            organizationUrl 'http://www.basho.com'
                        }
                        developer {
                            name 'Chris Mancini'
                            email 'cmancini@basho.com'
                            organization 'Basho Technologies, Inc'
                            organizationUrl 'http://www.basho.com'
                        }
                        developer {
                            name 'Sergey Galkin'
                            email 'sgalkin@basho.com'
                            organization 'Basho Technologies, Inc'
                            organizationUrl 'http://www.basho.com'
                        }
                    }

                    scm {
                        connection 'scm:git:ssh://github.com/basho/data_platform_riak_java_client.git'
                        developerConnection 'scm:git:ssh://github.com/basho/data_platform_riak_java_client.git'
                        url 'https://github.com/basho/data_platform_riak_java_client'
                        tag 'HEAD'
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = "$bashoArtifactoryUrl"
    publish {
        repository {
            repoKey = 'libs-snapshot-local'
            username = "$artifactoryUser"
            password = "$artifactoryPass"
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
        }
    }
    resolve {
        repository {
            repoKey = 'libs-snapshot-local'
            username = "$artifactoryUser"
            password = "$artifactoryPass"
        }
    }
    if (buildName) {
        clientConfig.info.buildName = buildName
        clientConfig.info.buildNumber = buildNum
        clientConfig.info.buildUrl = travisBuildLink
    }
}
